// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Tube Switch Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value0 : object.inTube // I dont really know what the point of this variable is but alright I guess

private alias 0 : TUBESWITCH_L
private alias 1 : TUBESWITCH_R

// Player stuff
private alias object.state     : player.state
private alias object.speed     : player.speed
private alias object.xvel      : player.xvel
private alias object.yvel      : player.yvel
private alias object.gravity   : player.gravity
private alias object.direction : player.direction
private alias object.animation : player.animation
private alias object.value34   : player.collisionDisabled

event ObjectMain
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		if player[currentPlayer].collisionDisabled == true
			player[currentPlayer].collisionDisabled = false
		end if

		CheckEqual(player[currentPlayer].state, PlayerObject_Death)
		temp0 = checkResult
		CheckEqual(player[currentPlayer].state, PlayerObject_KnuxLedgePullUp)
		temp0 |= checkResult
		CheckEqual(player[currentPlayer].state, PlayerObject_KnuxWallClimb)
		temp0 |= checkResult
		if temp0 == 0
			BoxCollisionTest(C_TOUCH, object.entityPos, -16, -16, 16, 16, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
			if checkResult == true

				if player[currentPlayer].yvel < 0
					player[currentPlayer].yvel = 0
				end if

				switch object.propertyValue
				case TUBESWITCH_L // Enter from the right, exit from the left
					if player[currentPlayer].xvel > 0
						if player[currentPlayer].animation != ANI_JUMPING
							if player[currentPlayer].state != PlayerObject_TubeSwitch
								PlaySfx(SfxName[Rolling], false)
							end if
						end if

						if player[currentPlayer].state == PlayerObject_KnuxGlideLeft
							FlipSign(player[currentPlayer].speed)
						end if

						player[currentPlayer].direction = FLIP_NONE
						player[currentPlayer].state 	= PlayerObject_TubeSwitch
						player[currentPlayer].animation = ANI_JUMPING
					else
						if player[currentPlayer].gravity == GRAVITY_GROUND
							if player[currentPlayer].xvel > -0x20000
								player[currentPlayer].speed = -0x20000
							end if
						end if

						player[currentPlayer].state = PlayerObject_HandleRoll
						player[currentPlayer].animation = ANI_JUMPING
					end if
					break

				case TUBESWITCH_R // Enter from the left, exit from the right
					if player[currentPlayer].xvel < 0
						if player[currentPlayer].animation != ANI_JUMPING
							if player[currentPlayer].state != PlayerObject_TubeSwitch
								PlaySfx(SfxName[Rolling], false)
							end if
						end if

						if player[currentPlayer].state == PlayerObject_KnuxGlideLeft
							FlipSign(player[currentPlayer].speed)
						end if

						player[currentPlayer].direction = FLIP_X
						player[currentPlayer].state 	= PlayerObject_TubeSwitch
						player[currentPlayer].animation = ANI_JUMPING
					else
						if player[currentPlayer].gravity == GRAVITY_GROUND
							if player[currentPlayer].xvel < 0x20000
								player[currentPlayer].speed = 0x20000
							end if
						end if

						player[currentPlayer].state = PlayerObject_HandleRoll
						player[currentPlayer].animation = ANI_JUMPING
					end if
					break

				end switch
				object.inTube = true
			else
				object.inTube = false
			end if
		end if
	next
end event

event RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			break
		case 0 // entryType
			checkResult = object.propertyValue
			break
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			object.propertyValue = editor.variableValue
			break
		case 0 // entryType
			object.propertyValue = editor.variableValue
			break
		end switch
	end if
end event

event RSDKDraw
	temp0 = object.xpos
	temp0 -= 0x80000
	temp1 = object.ypos
	temp1 -= 0x80000

	DrawSpriteXY(0, temp0, temp1)

	temp0 += 0x100000
	DrawSpriteXY(0, temp0, temp1)

	temp1 += 0x100000
	DrawSpriteXY(0, temp0, temp1)

	temp0 -= 0x100000
	DrawSpriteXY(0, temp0, temp1)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 239, 239)

	AddEditorVariable("entryType")
	SetActiveVariable("entryType")
	AddEnumVariable("Right", 0)
	AddEnumVariable("Left", 1)
end event
