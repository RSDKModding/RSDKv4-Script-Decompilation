// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Fall Platform Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================
private alias object.value0 : object.stood
private alias object.value1 : object.collisionOffset.y
private alias object.value2 : object.timer
private alias object.value3 : object.centerPos.y
private alias object.value4 : object.stoodAngle

private alias 0 : FALLPLATFORM_IDLE
private alias 1 : FALLPLATFORM_FALLING_SOLID
private alias 2 : FALLPLATFORM_FALLING
private alias 3 : FALLPLATFORM_OFFSCREEN

// Player Aliases
private alias object.ypos : player.ypos
private alias object.yvel : player.yvel
private alias object.gravity : player.gravity


// ========================
// Function Declarations
// ========================
reserve function FallPlatform_DebugDraw
reserve function FallPlatform_DebugSpawn


// ========================
// Function Definitions
// ========================

private function FallPlatform_DebugDraw
	DrawSprite(0)
end function


private function FallPlatform_DebugSpawn
	CreateTempObject(TypeName[Fall Platform], 0, object.xpos, object.ypos)
	object[tempObjectPos].centerPos.y = object[tempObjectPos].ypos
end function


// ========================
// Events
// ========================

event ObjectUpdate
	if object.stood == true
		if object.timer == 0
			object.priority = PRIORITY_ACTIVE
			object.timer = 30
		end if

		if object.stoodAngle < 64
			object.stoodAngle += 4
		end if
	else
		if object.stoodAngle > 0
			object.stoodAngle -= 4
		end if
	end if

	temp0 = object.ypos
	temp0 &= 0xFFFF0000

	switch object.state
	case FALLPLATFORM_IDLE
		temp1 = object.stoodAngle
		temp1 <<= 1
		Sin(temp2, temp1)
		temp2 <<= 9
		object.ypos = object.centerPos.y
		object.ypos += temp2
		if object.timer != 0
			object.timer--
			if object.timer == 0
				object.state++
				object.timer = 32
			end if
		end if
		break

	case FALLPLATFORM_FALLING_SOLID
		object.timer--
		if object.timer == 0
			object.state++
			foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
				BoxCollisionTest(C_PLATFORM, object.entityPos, -32, -8, 32, 0, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
				if checkResult == true
					player[currentPlayer].ypos += object.yvel
					player[currentPlayer].yvel = object.yvel
					player[currentPlayer].yvel -= 0x3800
					player[currentPlayer].gravity = GRAVITY_AIR
				end if
			next
		end if
		// [Fallthrough]
	case FALLPLATFORM_FALLING
		object.ypos += object.yvel
		object.yvel += 0x3800

		if object.outOfBounds == true
			object.ypos = object.centerPos.y
			object.yvel = 0
			object.timer = 0
			object.state++
		end if
		break

	case FALLPLATFORM_OFFSCREEN
		if object.outOfBounds == true
			object.state = FALLPLATFORM_IDLE
			object.priority = PRIORITY_BOUNDS
		end if
		break
	end switch

	object.collisionOffset.y = object.ypos
	object.collisionOffset.y &= 0xFFFF0000
	object.collisionOffset.y -= temp0
	object.stood = false

	if object.state < FALLPLATFORM_FALLING
		temp1 = object.ypos
		object.ypos = temp0
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_PLATFORM, object.entityPos, -32, -8, 32, 0, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
			if checkResult == true
				object.stood = true
				player[currentPlayer].ypos += object.collisionOffset.y
			end if
		next
		object.ypos = temp1
	end if
end event


event ObjectDraw
	if object.state < FALLPLATFORM_OFFSCREEN
		DrawSprite(0)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("SLZ/Objects.gif")
	SpriteFrame(-32, -8, 64, 32, 67, 26)

	foreach (TypeName[Fall Platform], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].centerPos.y = object[arrayPos0].ypos
	next

	SetTableValue(TypeName[Fall Platform], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(FallPlatform_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(FallPlatform_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("SLZ/Objects.gif")
	SpriteFrame(-32, -8, 64, 32, 67, 26)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
