// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Ball Hog Bomb Object
// Script Author: Christan Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

//-------Aliases-------//
private alias 45 : TYPE_BALLHOGBOMB


// Static Values

// Tables

event ObjectMain
	object.xpos += object.xvel
	object.ypos += object.yvel
	object.yvel += 0x3800
	if object.yvel > 0
		temp0 = object.ypos
		ObjectTileCollision(CSIDE_FLOOR, 0, 8, 0)
		if checkResult == 1
			object.yvel = -0x30000
			object.ypos = temp0
			ObjectTileGrip(CSIDE_FLOOR, -2, 8, 0)
			temp1 = object.ypos
			object.ypos = temp0
			ObjectTileGrip(CSIDE_FLOOR, 2, 8, 0)
			temp2 = object.ypos
			object.ypos = temp0
			if object.xvel > 0
				if temp2 < temp1
					FlipSign(object.xvel)
				end if
			else
				if temp1 < temp2
					FlipSign(object.xvel)
				end if
			end if
		end if
	end if
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -6, -6, 6, 6, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
		if checkResult == 1
			CallFunction(PlayerObject_Hit)
		end if
	next
	object.animationTimer++
	if object.animationTimer >= 6
		object.frame ^= 1
		object.animationTimer = 0
	end if
	if object.value0 > 0
		object.value0--
	else
		ResetObjectEntity(object.entityPos, TypeName[Blank Object], 0, object.xpos, object.ypos)
		CreateTempObject(TypeName[Explosion], 0, object.xpos, object.ypos)
		object[tempObjectPos].drawOrder = 4
		PlaySfx(SfxName[Explosion], 0)
	end if
end event


event ObjectDraw
	DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
end event


event ObjectStartup
	CheckCurrentStageFolder("Zone06")
	if checkResult == 1
		LoadSpriteSheet("SBZ/Objects.gif")
		SpriteFrame(-7, -7, 14, 14, 82, 126)
		SpriteFrame(-7, -7, 14, 14, 82, 143)
	end if
	CheckCurrentStageFolder("Zone07")
	if checkResult == 1
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-7, -7, 14, 14, 165, 248)
		SpriteFrame(-7, -7, 14, 14, 182, 248)
	end if
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
