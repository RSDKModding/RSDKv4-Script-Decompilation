// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Continue Screen Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 40 : TYPE_CONTINUESCREEN

private alias object.value0 : continue.countdown	// The time you see in big numbers (seconds)
private alias object.value1 : continue.timer		// The hidden in-between timer (frames)

event ObjectMain

	if object.state == 1
		continue.timer--
		if continue.timer <= 0
			if continue.countdown == 0
				object.state++
				object[-2].state = 7
			else
				continue.countdown--
				continue.timer = 60
			end if
		end if
	end if

end event


event ObjectDraw

	DrawSprite(0)
	DrawSprite(1)

	temp0 = object.xpos
	temp0 >>= 16
	DrawNumbers(2, temp0, 126, continue.countdown, 2, 8, 1)

	temp0 = player.continues
	temp1 = temp0
	temp0 -= 2
	temp0 *= 21
	temp0 >>= 1
	FlipSign(temp0)
	temp0 += screen.xcenter
	GetBit(object.frame, oscillation, 4)
	object.frame += 12
	temp3 = 1
	while temp1 > 1
		temp2 = 1
		if temp3 == 1
			temp3 = 0
			if object[-2].state >= 5
				if object[-2].state != 7
					temp2 = 0
				end if
			else
				if object[-2].state > 3
					GetBit(temp2, oscillation, 0)
				end if
			end if
		end if
		if temp2 == 1
			DrawSpriteScreenXY(object.frame, temp0, 100)
		end if
		temp0 += 21
		temp1--
	loop

end event


event ObjectStartup

	LoadSpriteSheet("Special/Objects.gif")

	// Sprite frames
	SpriteFrame(-59, 64, 118, 16, 382, 400)
	SpriteFrame(-21, 128, 41, 9, 357, 390)
	SpriteFrame(0, 0, 8, 11, 120, 418)
	SpriteFrame(0, 0, 8, 11, 129, 418)
	SpriteFrame(0, 0, 8, 11, 138, 418)
	SpriteFrame(0, 0, 8, 11, 147, 418)
	SpriteFrame(0, 0, 8, 11, 120, 430)
	SpriteFrame(0, 0, 8, 11, 129, 430)
	SpriteFrame(0, 0, 8, 11, 138, 430)
	SpriteFrame(0, 0, 8, 11, 147, 430)
	SpriteFrame(0, 0, 8, 11, 120, 442)
	SpriteFrame(0, 0, 8, 11, 129, 442)

	switch stage.playerListPos
	case PlayerName[SONIC]
	case PlayerName[SONIC AND TAILS]
		SpriteFrame(-8, -11, 16, 23, 399, 376)
		SpriteFrame(-8, -11, 16, 23, 416, 376)
		break

	case PlayerName[TAILS]
		SpriteFrame(-8, -11, 16, 23, 433, 376)
		SpriteFrame(-8, -11, 16, 23, 450, 376)
		break

	case PlayerName[KNUCKLES]
		SpriteFrame(-8, -11, 16, 23, 467, 376)
		SpriteFrame(-8, -11, 16, 23, 484, 376)
		break

	end switch

	foreach (TypeName[Continue Screen], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].drawOrder = 5
		object[arrayPos0].xpos = screen.xcenter
		object[arrayPos0].xpos <<= 16
		object[arrayPos0].ypos = 0
		object[arrayPos0].value0 = 10
	next
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
