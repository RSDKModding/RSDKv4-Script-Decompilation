// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Falling Block Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================
private alias object.value0 : object.stood
private alias object.value1 : object.collisionOffset.y
private alias object.value2 : object.timer
private alias object.value3 : object.startPos.y
private alias object.value4 : object.stoodAngle

private alias 0 : FALLINGBLOCK_IDLE
private alias 1 : FALLINGBLOCK_MOVING
private alias 2 : FALLINGBLOCK_RETURNTOSTART
private alias 3 : FALLINGBLOCK_OOB

// Player Aliases
private alias object.ypos : player.ypos

// ========================
// Function Declarations
// ========================
reserve function FallingBlock_DebugDraw
reserve function FallingBlock_DebugSpawn


// ========================
// Tables
// ========================

private function FallingBlock_DebugDraw
	DrawSprite(0)
end function


private function FallingBlock_DebugSpawn
	CreateTempObject(TypeName[Falling Block], 0, object.xpos, object.ypos)
	object[tempObjectPos].startPos.y = object.ypos
end function


// ========================
// Events
// ========================

event ObjectUpdate
	if object.stood == true
		if object.timer == 0
			object.timer = 30
		end if

		if object.stoodAngle < 64
			object.stoodAngle += 4
		end if
	else
		if object.stoodAngle > 0
			object.stoodAngle -= 4
		end if
	end if

	temp0 = object.ypos
	temp0 &= 0xFFFF0000

	switch object.state
	case FALLINGBLOCK_IDLE
		temp1 = object.stoodAngle
		temp1 <<= 1
		Sin(temp2, temp1)
		temp2 <<= 9
		object.ypos = object.startPos.y
		object.ypos += temp2
		if object.timer != 0
			object.timer--
			if object.timer == 0
				object.state++
				object.priority = PRIORITY_ACTIVE
			end if
		end if
		break

	case FALLINGBLOCK_MOVING
		object.ypos += object.yvel
		object.yvel += 0x800
		ObjectTileCollision(0, 0, 16, 0)
		if checkResult == true
			object.state++
		end if

		if object.outOfBounds == true
			object.ypos = object.startPos.y
			object.yvel = 0
			object.timer = 0
			object.state = FALLINGBLOCK_OOB
		end if
		break

	case FALLINGBLOCK_RETURNTOSTART
		if object.outOfBounds == true
			object.ypos = object.startPos.y
			object.yvel = 0
			object.timer = 0
			object.state++
		end if
		break

	case FALLINGBLOCK_OOB
		if object.outOfBounds == true
			object.state = FALLINGBLOCK_IDLE
			object.priority = PRIORITY_BOUNDS
		end if
		break
	end switch

	object.collisionOffset.y = object.ypos
	object.collisionOffset.y &= 0xFFFF0000
	object.collisionOffset.y -= temp0
	object.stood = false

	if object.state < FALLINGBLOCK_OOB
		temp1 = object.ypos
		object.ypos = temp0
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_SOLID, object.entityPos, -16, -16, 16, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
			if checkResult == COL_TOP
				object.stood = true
				player[currentPlayer].ypos += object.collisionOffset.y
			end if
		next
		object.ypos = temp1
	end if
end event


event ObjectDraw
	if object.state < FALLINGBLOCK_OOB
		DrawSprite(0)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("LZ/Objects.gif")
	SpriteFrame(-16, -16, 32, 32, 84, 223)

	foreach (TypeName[Falling Block], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].startPos.y = object[arrayPos0].ypos
	next

	SetTableValue(TypeName[Falling Block], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(FallingBlock_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(FallingBlock_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("LZ/Objects.gif")
	SpriteFrame(-16, -16, 32, 32, 84, 223)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
