// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: HUD Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 5 : TYPE_HUD

private alias object.value0 : HUD.drawFunction
private alias object.value1 : HUD.flashingTimer

private alias object.xpos 	: player.xpos
private alias object.ypos 	: player.ypos
private alias object.value0 : player.rings

// Function declarations
reserve function HUD_DrawPC
reserve function HUD_DrawMobile


function HUD_DrawPC

	DrawSpriteScreenXY(10, 17, 13)

	if stage.minutes == 9
		if HUD.flashingTimer > 7
			DrawSpriteScreenXY(11, 17, 29)
		end if
	end if

	if player[0].rings == 0
		if HUD.flashingTimer > 7
			DrawSpriteScreenXY(12, 17, 45)
		end if
	end if

	DrawNumbers(0, 104, 13, player.score, 6, 8, 0)

	if options.gameMode < 2
		// Normal game - only minutes and seconds are shown
		DrawSpriteScreenXY(14, 67, 29)
	else
		// Time attack - draw milliseconds, ontop of the other values
		DrawSpriteScreenXY(13, 67, 29)
		DrawNumbers(0, 104, 29, stage.milliSeconds, 2, 8, 1)
	end if

	DrawNumbers(0, 80, 29, stage.seconds, 2, 8, 1)
	DrawNumbers(0, 56, 29, stage.minutes, 1, 8, 1)

	DrawNumbers(0, 80, 45, player[0].rings, 3, 8, 0)

	if stage.debugMode == true

		// Player position display, show it if they have debug mode on

		// XPos display
		temp0 = player[0].xpos
		temp0 >>= 16
		Abs(temp0)
		temp1 = screen.xsize
		temp1 -= 24
		DrawNumbers(0, temp1, 13, temp0, 5, 8, 1)

		// YPos display
		temp0 = player[0].ypos
		temp0 >>= 16
		Abs(temp0)
		DrawNumbers(0, temp1, 29, temp0, 5, 8, 1)

		// Move 42 pixels to the left
		temp1 -= 42

		// Draw the "X:" and "Y:" icons

		// Use red variant if player is in a negative pos
		if player[0].xpos >= 0
			DrawSpriteScreenXY(18, temp1, 15)
		else
			DrawSpriteScreenXY(36, temp1, 15)
		end if

		if player[0].ypos >= 0
			DrawSpriteScreenXY(19, temp1, 31)
		else
			DrawSpriteScreenXY(37, temp1, 31)
		end if
	end if

	// Lives icon
	temp0 = stage.playerListPos
	temp0 += 15
	DrawSpriteScreenXY(temp0, 16, 212)

	// Character text
	temp0 += 6
	DrawSpriteScreenXY(temp0, 33, 213)

	// Small "x" inbetween icon and text
	DrawSpriteScreenXY(20, 38, 222)

	// The actual player lives number
	DrawNumbers(24, 56, 220, player.lives, 2, 8, 0)

end function


function HUD_DrawMobile

	DrawSpriteScreenXY(10, 17, 9)

	if stage.minutes == 9
		if HUD.flashingTimer > 7
			DrawSpriteScreenXY(11, 17, 25)
		end if
	end if

	if player[0].rings == 0
		if HUD.flashingTimer > 7
			DrawSpriteScreenXY(12, 17, 41)
		end if
	end if

	DrawNumbers(0, 104, 9, player.score, 6, 8, 0)

	if options.gameMode < 2
		DrawSpriteScreenXY(14, 67, 25)
	else
		DrawSpriteScreenXY(13, 67, 25)
		DrawNumbers(0, 104, 25, stage.milliSeconds, 2, 8, 1)
	end if

	DrawNumbers(0, 80, 25, stage.seconds, 2, 8, 1)
	DrawNumbers(0, 56, 25, stage.minutes, 1, 8, 1)
	DrawNumbers(0, 80, 41, player[0].rings, 3, 8, 0)

	if object[0].type == TypeName[Debug Mode]
		// Player position display, only show it when the player is in debug mode
		temp0 = object[0].xpos
		temp0 >>= 16
		Abs(temp0)
		temp1 = screen.xsize
		temp1 -= 16
		DrawNumbers(0, temp1, 25, temp0, 5, 8, 1)
		temp0 = object[0].ypos
		temp0 >>= 16
		Abs(temp0)
		DrawNumbers(0, temp1, 41, temp0, 5, 8, 1)
		temp1 -= 42
		if object[0].xpos >= 0
			DrawSpriteScreenXY(18, temp1, 27)
		else
			DrawSpriteScreenXY(36, temp1, 27)
		end if
		if object[0].ypos >= 0
			DrawSpriteScreenXY(19, temp1, 43)
		else
			DrawSpriteScreenXY(37, temp1, 43)
		end if
		DrawSpriteScreenXY(34, screen.xcenter, 16)
		DrawSpriteScreenXY(35, screen.xcenter, 16)
	end if
	temp1 = screen.xsize
	temp1 -= 64
	temp0 = stage.playerListPos
	temp0 += 15
	DrawSpriteScreenXY(temp0, temp1, 8)
	temp1 += 17
	temp0 += 6
	DrawSpriteScreenXY(temp0, temp1, 9)
	temp1 += 5
	DrawSpriteScreenXY(20, temp1, 18)
	temp1 += 18
	DrawNumbers(24, temp1, 16, player.lives, 2, 8, 0)
end function


event ObjectDraw

	if player.rings[0] == 0
		HUD.flashingTimer++

		if HUD.flashingTimer == 16
			HUD.flashingTimer = 0
		end if
	else
		if stage.minutes == 9
			HUD.flashingTimer++

			if HUD.flashingTimer == 16
				HUD.flashingTimer = 0
			end if
		end if
	end if

	CallFunction(HUD.drawFunction)

end event


event ObjectStartup

	LoadSpriteSheet("Global/Display.gif")

	// HUD frames
	SpriteFrame(0, 0, 8, 11, 1, 50) 	// 0  - Number 0
	SpriteFrame(0, 0, 8, 11, 10, 50) 	// 1  - Number 1
	SpriteFrame(0, 0, 8, 11, 19, 50) 	// 2  - Number 2
	SpriteFrame(0, 0, 8, 11, 28, 50) 	// 3  - Number 3
	SpriteFrame(0, 0, 8, 11, 1, 62) 	// 4  - Number 4
	SpriteFrame(0, 0, 8, 11, 10, 62) 	// 5  - Number 5
	SpriteFrame(0, 0, 8, 11, 19, 62) 	// 6  - Number 6
	SpriteFrame(0, 0, 8, 11, 28, 62) 	// 7  - Number 7
	SpriteFrame(0, 0, 8, 11, 1, 74) 	// 8  - Number 8
	SpriteFrame(0, 0, 8, 11, 10, 74) 	// 9  - Number 9
										// (A-F characters next to those are unused)
	SpriteFrame(0, 0, 40, 43, 1, 1) 	// 10 - Score, Times, Ring text
	SpriteFrame(0, 0, 31, 11, 1, 119) 	// 11 - Red "Time" text
	SpriteFrame(0, 0, 39, 11, 1, 131) 	// 12 - Red "Rings" text
	SpriteFrame(0, 0, 29, 4, 1, 45) 	// 13 - Timer ticks
	SpriteFrame(0, 0, 3, 11, 37, 50) 	// 14 - Colon
	SpriteFrame(0, 0, 16, 16, 83, 18) 	// 15 - Sonic life icon
	SpriteFrame(0, 0, 16, 16, 100, 18) 	// 16 - Tails life icon
	SpriteFrame(0, 0, 16, 16, 117, 18) 	// 17 - Knuckles life icon
	SpriteFrame(0, 0, 8, 8, 1, 98) 		// 18 - Debug "X"
	SpriteFrame(0, 0, 8, 8, 10, 98) 	// 19 - Debug "Y"
	SpriteFrame(0, 0, 6, 6, 19, 98) 	// 20 - Debug small *
	SpriteFrame(0, 0, 31, 7, 185, 18) 	// 21 - "Sonic" life icon text

	// Tails or Miles
	if options.region == false
		SpriteFrame(0, 0, 31, 7, 217, 18)  	// 22 - "Tails" life icon text
	else
		SpriteFrame(0, 0, 31, 7, 185, 26) 	// 22 - "Miles" life icon text
	end if

	SpriteFrame(0, 0, 31, 7, 217, 26) 	// 23 - "K-T-E" life icon text
	SpriteFrame(0, 0, 8, 8, 37, 64) 	// 24 - Life icon number 0
	SpriteFrame(0, 0, 8, 8, 46, 64) 	// 25 - Life icon number 1
	SpriteFrame(0, 0, 8, 8, 55, 64) 	// 26 - Life icon number 2
	SpriteFrame(0, 0, 8, 8, 64, 64) 	// 27 - Life icon number 3
	SpriteFrame(0, 0, 8, 8, 73, 64) 	// 28 - Life icon number 4
	SpriteFrame(0, 0, 8, 8, 82, 64) 	// 29 - Life icon number 5
	SpriteFrame(0, 0, 8, 8, 91, 64) 	// 30 - Life icon number 6
	SpriteFrame(0, 0, 8, 8, 100, 64) 	// 31 - Life icon number 7
	SpriteFrame(0, 0, 8, 8, 109, 64) 	// 32 - Life icon number 8
	SpriteFrame(0, 0, 8, 8, 118, 64) 	// 33 - Life icon number 9
	SpriteFrame(-16, 0, 8, 8, 127, 64) 	// 34 - Debug minus icon
	SpriteFrame(8, 0, 8, 8, 136, 64) 	// 35 - Debug plus icon
	SpriteFrame(0, 0, 8, 8, 25, 98) 	// 36 - Debug red "X"
	SpriteFrame(0, 0, 8, 8, 34, 98) 	// 37 - Debug red "Y"

	// Set the HUD's draw function based on the current platform
	foreach (TypeName[HUD], arrayPos0, ALL_ENTITIES)
		if engine.deviceType == RETRO_STANDARD
			HUD[arrayPos0].drawFunction = HUD_DrawPC
		else
			HUD[arrayPos0].drawFunction = HUD_DrawMobile
		end if
	next

end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
