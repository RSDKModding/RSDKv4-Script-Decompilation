// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Dust Puff Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 20 : TYPE_DUSTPUFF

private alias object.propertyValue : object.targetPlayer


event ObjectMain

	// Update object movement
	object.xpos += object.xvel
	object.ypos += object.yvel

	switch object.frame
	case 0
	case 1
	case 2
		object.animationTimer++
		if object.animationTimer > 3
			object.frame++
			object.animationTimer = 0
		end if
		break

	case 3
		object.animationTimer++
		if object.animationTimer > 3
			object.type = TypeName[Blank Object]
		end if
		break

	case 4
	case 5
	case 6
	case 7
	case 8
	case 9
		object.animationTimer++
		if object.animationTimer > 1
			object.frame++
			object.animationTimer = 0
		end if
		break

	case 10
		object.animationTimer++
		if object.animationTimer > 1
			object.frame = 4
			object.animationTimer = 0
		end if
		break

	end switch

end event


event ObjectDraw

	if object.frame > 3
		// Spindash dust variant

		// Seek into this object's targeted player
		currentPlayer = object.targetPlayer

		// Make the values match
		object.direction = object[currentPlayer].direction
		object.xpos = object[currentPlayer].xpos
		object.iypos = object[currentPlayer].collisionBottom
		object.ypos += object[currentPlayer].ypos

		// Erase this object if the pllayer isn't spindashing
		if object[currentPlayer].animation != ANI_SPINDASH
			object.type = TypeName[Blank Object]
		else
			DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
		end if
	else

		// Dust Puff variant (from skidding and etc.)
		DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)

	end if
end event


event ObjectStartup

	LoadSpriteSheet("Global/Items.gif")

	// Dust frames
	SpriteFrame(-7, -11, 13, 14, 85, 143)
	SpriteFrame(-7, -11, 13, 14, 99, 143)
	SpriteFrame(-6, -11, 13, 14, 113, 143)
	SpriteFrame(-7, -11, 13, 14, 127, 143)
	SpriteFrame(-32, -10, 32, 10, 58, 245)
	SpriteFrame(-32, -12, 32, 12, 91, 243)
	SpriteFrame(-32, -16, 32, 16, 124, 239)
	SpriteFrame(-32, -18, 32, 18, 157, 237)
	SpriteFrame(-32, -20, 32, 20, 190, 235)
	SpriteFrame(-32, -21, 32, 21, 223, 210)
	SpriteFrame(-32, -23, 32, 23, 223, 232)
	
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
