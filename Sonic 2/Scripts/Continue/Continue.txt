// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Continue Screen Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 41 : TYPE_CONTINUESCREEN

public alias object.value0 : continueSetup.timerSeconds
public alias object.value1 : continueSetup.timerFrames

event ObjectMain
	if object.state == 1
		continueSetup.timerFrames--
		if continueSetup.timerFrames <= 0
			if continueSetup.timerSeconds == 0
				object.state++
				object[+1].state = 6 // Tell the main object that time is up
			else
				continueSetup.timerSeconds--
				continueSetup.timerFrames = 60
			end if
		end if
	end if
end event


event ObjectDraw
	DrawSprite(0)
	DrawSprite(1)
	temp0 = object.xpos
	temp0 >>= 16
	DrawNumbers(2, temp0, 126, continueSetup.timerSeconds, 2, 8, 1)
	temp0 = player.continues
	temp1 = temp0
	temp0 -= 2
	temp0 *= 21
	temp0 >>= 1
	FlipSign(temp0)
	temp0 += screen.xcenter
	GetBit(object.frame, oscillation, 4)
	object.frame += 12
	temp3 = 1
	while temp1 > 1
		temp2 = 1
		if temp3 == 1
			temp3 = 0
			if object[+1].state >= 4
				if object[+1].state != 6
					temp2 = 0
				end if
			else
				if object[+1].state > 2
					GetBit(temp2, oscillation, 0)
				end if
			end if
		end if
		if temp2 == 1
			DrawSpriteScreenXY(object.frame, temp0, 100)
		end if
		temp0 += 21
		temp1--
	loop
end event


event ObjectStartup
	LoadSpriteSheet("Continue/Objects.gif")
	SpriteFrame(-59, 64, 119, 16, 1, 1)
	SpriteFrame(-21, 128, 41, 9, 1, 42)
	SpriteFrame(0, 0, 8, 11, 1, 52)
	SpriteFrame(0, 0, 8, 11, 10, 52)
	SpriteFrame(0, 0, 8, 11, 19, 52)
	SpriteFrame(0, 0, 8, 11, 28, 52)
	SpriteFrame(0, 0, 8, 11, 37, 52)
	SpriteFrame(0, 0, 8, 11, 46, 52)
	SpriteFrame(0, 0, 8, 11, 55, 52)
	SpriteFrame(0, 0, 8, 11, 64, 52)
	SpriteFrame(0, 0, 8, 11, 73, 52)
	SpriteFrame(0, 0, 8, 11, 82, 52)
	switch stage.playerListPos
	case 0
	case 3
		SpriteFrame(-8, -11, 16, 23, 1, 18)
		SpriteFrame(-8, -11, 16, 23, 18, 18)
		break
	case 1
		SpriteFrame(-8, -11, 16, 23, 35, 18)
		SpriteFrame(-8, -11, 16, 23, 52, 18)
		break
	case 2
		SpriteFrame(-8, -11, 16, 23, 69, 18)
		SpriteFrame(-8, -11, 16, 23, 86, 18)
		break
	end switch
	foreach (TypeName[Continue Screen], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].drawOrder = 5
		object[arrayPos0].xpos = screen.xcenter
		object[arrayPos0].xpos <<= 16
		object[arrayPos0].ypos = 0
		continueSetup.timerSeconds[arrayPos0] = 10
		continueSetup.timerFrames[arrayPos0] = 60
	next
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
