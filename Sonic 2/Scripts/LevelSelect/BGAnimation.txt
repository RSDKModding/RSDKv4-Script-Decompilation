// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: BG Animation Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================
private alias object.value0 	   : object.timer
private alias object.propertyValue : object.enabled

// ========================
// Static Values
// ========================
public value BGAnimation_currentPreview = 0

// Kept here for mod compatability reasons, please don't use this if possible
private alias BGAnimation_currentPreview : BGAnimation_value0

event ObjectMain
	object.timer++
	object.timer &= 0x1FF

	Sin(temp0, object.timer)
	if temp0 > 0
		if temp0 < 32
			screen.yoffset = 256
		else
			screen.yoffset = 0
		end if
	else
		if temp0 > -32
			screen.yoffset = 512
		else
			screen.yoffset = 768
		end if
	end if
end event


event ObjectDraw
	if object.enabled == false
		// This script doesn't only do BG animation, it also deals with drawing the zone preview at the bottom right of the screen
		// BGAnimation_currentPreview is set by the Menu Control object
		temp0 = screen.xcenter
		temp0 -= 180
		if temp0 > 0
			temp0 = 0
		end if
		temp0 += screen.xcenter
		DrawSpriteScreenXY(14, temp0, 198)
		DrawSpriteScreenXY(BGAnimation_currentPreview, temp0, 198)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("LevelSelect/Icons.gif")

	// Set all BG Animation objects to active
	// (There should only ever be one in a scene, though)
	foreach (TypeName[BG Animation], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].priority = PRIORITY_ACTIVE
	next

	SpriteFrame(56, -12, 32, 24, 0, 50) 	// Sonic headset gaming music thing	- #0
	
	// 1-13 - Zone previews
	SpriteFrame(56, -12, 32, 24, 1, 85)		// EHZ								- #1
	SpriteFrame(56, -12, 32, 24, 34, 85)	// CPZ								- #2
	SpriteFrame(56, -12, 32, 24, 67, 85)	// ARZ								- #3
	SpriteFrame(56, -12, 32, 24, 100, 85)	// CNZ								- #4
	SpriteFrame(56, -12, 32, 24, 133, 85)	// HTZ								- #5
	SpriteFrame(56, -12, 32, 24, 166, 85)	// MCZ								- #6
	SpriteFrame(56, -12, 32, 24, 199, 85)	// HPZ								- #7
	SpriteFrame(56, -12, 32, 24, 180, 1)	// OOZ								- #8
	SpriteFrame(56, -12, 32, 24, 213, 1)	// MTZ								- #9
	SpriteFrame(56, -12, 32, 24, 180, 26)	// SCZ								- #10
	SpriteFrame(56, -12, 32, 24, 213, 26)	// WFZ								- #11
	SpriteFrame(56, -12, 32, 24, 180, 51)	// DEZ								- #12
	SpriteFrame(56, -12, 32, 24, 213, 51)	// Special Stage					- #13

	// Selection frame - #14
	SpriteFrame(32, -24, 80, 50, 0, 0)

	// Default to the EHZ icon
	BGAnimation_currentPreview = 1
end event


event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
