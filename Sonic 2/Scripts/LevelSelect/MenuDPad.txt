// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Menu DPad Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================
// value0 is skipped over
private alias object.value1 : object.pressedLR

// Kept here for mod compatability reasons, please don't use this if possible
private alias MenuControl_value2 : MenuControl_optionsMenuOpen


// ========================
// Events
// ========================

event ObjectUpdate
	if MenuControl_optionsMenuOpen == false
		temp0 = screen.xcenter
		temp0 -= 56
		CheckTouchRect(0, 96, temp0, screen.ysize)
		if checkResult > -1
			arrayPos0 = checkResult
			temp0 = touchscreen[arrayPos0].xpos
			temp0 -= 64
			temp1 = touchscreen[arrayPos0].ypos
			temp1 -= 202
			ATan2(temp2, temp0, temp1)
			temp2 += 0x20
			temp2 &= 0xFF
			temp2 >>= 6
			switch temp2
			case 0
				inputDown.right = true
				if object.pressedLR == false
					inputPress.right |= inputDown.right
				end if
				object.pressedLR = inputDown.right
				break

			case 1
				inputDown.down = true
				break

			case 2
				inputDown.left = true
				if object.pressedLR == false
					inputPress.left |= inputDown.left
				end if
				object.pressedLR = inputDown.left
				break

			case 3
				inputDown.up = true
				break
			end switch
		else
			object.pressedLR = false
		end if
	end if
end event


event ObjectDraw
	if MenuControl_optionsMenuOpen == false
		if inputDown.up == true
			DrawSpriteScreenXY(1, 64, 202)
		else
			DrawSpriteScreenXY(0, 64, 202)
		end if
	
		if inputDown.down == true
			DrawSpriteScreenXY(3, 64, 202)
		else
			DrawSpriteScreenXY(2, 64, 202)
		end if
	
		if inputDown.left == true
			DrawSpriteScreenXY(5, 64, 202)
		else
			DrawSpriteScreenXY(4, 64, 202)
		end if
	
		if inputDown.right == true
			DrawSpriteScreenXY(7, 64, 202)
		else
			DrawSpriteScreenXY(6, 64, 202)
		end if
	end if
end event


event ObjectStartup
	LoadSpriteSheet("LevelSelect/Icons.gif")
	
	foreach (TypeName[Menu DPad], arrayPos0, ALL_ENTITIES)
		switch engine.deviceType
		case RETRO_STANDARD
			// If on a standard platform, then touch controls aren't needed
			object[arrayPos0].type = TypeName[Blank Object]
			break

		case RETRO_MOBILE
			object[arrayPos0].priority = PRIORITY_ACTIVE
			break
		end switch
	next

	SpriteFrame(-10, -30, 20, 20, 138, 1)	// 0 - Blue up arrow
	SpriteFrame(-10, -30, 20, 20, 138, 43)	// 1 - Red up arrow
	SpriteFrame(-10, 10, 20, 20, 159, 1)	// 2 - Blue down arrow
	SpriteFrame(-10, 10, 20, 20, 159, 43)	// 3 - Red down arrow
	SpriteFrame(-30, -10, 20, 20, 138, 22)	// 4 - Blue left arrow
	SpriteFrame(-30, -10, 20, 20, 138, 64)	// 5 - Red left arrow
	SpriteFrame(10, -10, 20, 20, 159, 22)	// 6 - Blue right arrow
	SpriteFrame(10, -10, 20, 20, 159, 64)	// 7 - Red right arrow
end event


event RSDKDraw
	DrawSpriteXY(0, object.xpos, object.ypos)

	DrawSpriteXY(2, object.xpos, object.ypos)

	DrawSpriteXY(4, object.xpos, object.ypos)

	DrawSpriteXY(6, object.xpos, object.ypos)
end event


event RSDKLoad
	LoadSpriteSheet("LevelSelect/Icons.gif")

	SpriteFrame(-10, -30, 20, 20, 138, 1)	// 0 - Blue up arrow
	SpriteFrame(-10, -30, 20, 20, 138, 43)	// 1 - Red up arrow
	SpriteFrame(-10, 10, 20, 20, 159, 1)	// 2 - Blue down arrow
	SpriteFrame(-10, 10, 20, 20, 159, 43)	// 3 - Red down arrow
	SpriteFrame(-30, -10, 20, 20, 138, 22)	// 4 - Blue left arrow
	SpriteFrame(-30, -10, 20, 20, 138, 64)	// 5 - Red left arrow
	SpriteFrame(10, -10, 20, 20, 159, 22)	// 6 - Blue right arrow
	SpriteFrame(10, -10, 20, 20, 159, 64)	// 7 - Red right arrow
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event